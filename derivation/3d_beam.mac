/* N2(zeta), slide 59 */
N1(zeta):= 1-zeta;
N2(zeta):= zeta;
N3(zeta):= 1-3*zeta^2+2*zeta^3;
N4(zeta):= -l*(zeta-2*zeta^2+zeta^3);
N5(zeta):= 3*zeta^2-2*zeta^3;
N6(zeta):= -l*(-zeta^2+zeta^3);
/*                 ux1,      uy1,      uz1,      rx1,      ry1,      rz1,       ux2,      uy2,      uz2,      rx2,      ry2      rz2                  */
Ne(zeta):= matrix([N1(zeta), 0,        0,        0,        0,        0,         N2(zeta), 0,        0,        0,        0,       0],
                  [0,        N3(zeta), 0,        0,        0,        -N4(zeta), 0,        N5(zeta), 0,        0,        0,       -N6(zeta)],
                  [0,        0,        N3(zeta), 0,        N4(zeta), 0,         0,        0,        N5(zeta), 0,        N6(zeta), 0],
                  [0,        0,        0,        N1(zeta), 0,        0,         0,        0,        0,        N2(zeta), 0,        0]
                 );
Me: integrate(transpose(Ne(zeta)).Ne(zeta), zeta, 0, 1);

depends(w, x1);
/* u, theta: Verschiebung */
/* w: Biegelinie */
/* 2.63, 2.52, 2.55 S 129*/
/* 4.86, S. 131 */
u1: w[1] + x2*(-diff(w[2], x1) + diff(w[1], x1)*diff(w[2], x1));
u2: w[2]-1/2*x2*diff(w[2], x1)^2;
/* G11, G12, G13: 4.105, S. 136 */
/* application of 2.252 to 8.86 without torsion (5.111, S. 196 ?) */
G11: expand(diff(u1, x1) + 1/2*(diff(u1, x1)*diff(u1, x1) + diff(u2, x1)*diff(u2, x1)));

/* nach 5.114 S. 196 (ErlÃ¤uterung 4.173) */
G11: diff(w[1], x1) - x2*diff(w[2], x1, 2) + 1/2*diff(w[2], x1)^2;

deriv_w_list:flatten( makelist(makelist(diff(w[i], x1, j), j, derivdegree(G11, w[i], x1)), i, 2));
w_order(t):= lsum(i, i, makelist(hipow(t, w_), w_, deriv_w_list));
G11L: map(lambda([t], if w_order(t)>1 then 0 else t), G11);

opL(G, w_):= matrix(makelist(subst(makelist(w[j]= w_[j, i], j, 2), G), i, length(transpose(w_))));
BL_: opL(G11L, Ne(x1/l));
BL: ev(BL_, diff);

K: E*ratsubst(A, x2*x3, ratsubst(0, integrate(x2, x2)*x3, ratsubst(J33, integrate(x2^2, x2)*x3, integrate(integrate(integrate(transpose(BL).BL, x1, 0, l), x2), x3))));

C1e: A*rho*integrate(Ne(x1/l), x1, 0, l);

for i: 1 thru 3 do for j: 1 thru 3 do (sprint("C3{", i, ",", j, "}=", ssubst("];[", "],[", string(args(integrate(transpose(row(Ne(x1/l), i)).row(Ne(x1/l), j), x1, 0, l)))), ";"), newline())$
    
G11N: map(lambda([t], if w_order(t)#2 then 0 else t), G11);
w_term2_coeffs(G):= funmake('matrix, makelist(makelist(coeff(G, deriv_w_list[i]*deriv_w_list[j]), i, length(deriv_w_list)), j, length(deriv_w_list)));

opL2(derivs, coeffs, w_):= funmake('matrix, makelist(makelist(sum(sum(subst([w[args(args(derivs[i])[1])[1]]= w_[args(args(derivs[i])[1])[1], a]], derivs[i])*subst([w[args(args(derivs[j])[1])[1]]= w_[args(args(derivs[j])[1])[1], b]],
 derivs[j])*coeffs[i, j], i, 1, length(derivs)), j, 1, length(derivs)), a, length(transpose(w_))), b, length(transpose(w_))));

/* 5.116 S. 197 */
dBNdz: opL2(deriv_w_list, w_term2_coeffs(2*G11N), Ne(x1/l));
dBNdz: expand(ev(dBNdz, diff));

/* 5.205 S.222 5.212 S.223 */
Ksigma_hat: A*integrate(dBNdz, x1, 0, l);

ssubst("];[", "],[", string(args(Ksigma_hat)));
ssubst("];[", "],[", string(args(subst(0, x2, BL))));

